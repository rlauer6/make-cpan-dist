#!@perlrun@

package CPAN::Maker;

# a CPAN distribution creation utility

use strict;
use warnings;

use CPAN::Maker::Constants qw( :all );
use CPAN::Maker::Utils qw( :all );

use Carp;
use Cwd;
use Data::Dumper;
use English qw( -no_match_vars );
use ExtUtils::MM;
use File::Basename;
use File::Find;
use File::Process qw( process_file filter );
use File::Temp qw( :POSIX );
use Getopt::Long qw(:config no_ignore_case );
use JSON::PP qw( encode_json decode_json );
use List::Util qw( pairs );
use Log::Log4perl qw( :easy );
use Log::Log4perl::Level;
use Pod::Usage;
use Scalar::Util qw( reftype );
use YAML::Tiny;
use version;

our $VERSION = '@PACKAGE_VERSION@'; ## no critic (RequireInterpolationOfMetachars)

caller or __PACKAGE__->main();

########################################################################
sub help {
########################################################################
  my ($options) = @_;

  my $token;

  if ( $options->{pager} ) {
    $token = eval {
      require IO::Pager;

      IO::Pager::open( *STDOUT, '|-:utf8', 'Unbuffered' );
    };
  }

  print <<"END_OF_TEXT";
usage: $PROGRAM_NAME OPTIONS

Example: $PROGRAM_NAME -m Some::Module -a 'Rob Lauer <rlauer6\@comcast.net>' -d 'my wonderful module'

OPTIONS
-------
-c, --cleanup             cleanup files, this is default
    --no-cleanup
-a, --author              author
-A, --any                 do not require a version
-B, --build-requires      build dependencies
-b, --buildspec           read a buildspec and create command line
-d, --abstract            abstract
-l, --log-level           ERROR, WARN, INFO, DEBUG, TRACE
-m, --module              module name
-M, --module-list         list of modules to include in distribution
-p, --project-root        default: current working directory
-P, --pager, --nopager    use a pager for help, default: use pager
-r, --requires            dependency list
-R, --require-versions    add version numbers to dependencies
    --no-require-versions
-t, --test-requires       test dependencies
-s, --scandeps            use scandeps for dependency checking
-v, --version             version

Typically, this script is called from the bash helper script of the same
name (make-cpan-dist) or is invoked with a YAML file that specifies
the parmeters used to build a distribution.

See https://github.com/rlauer6/make-cpan-dist.git for more documentation.

See man make-cpan-dist

Version: $VERSION

END_OF_TEXT
  return $SH_FAILURE;
}

########################################################################
sub _is_obj {
########################################################################
  my ( $this, $type ) = @_;

  return ref $this && reftype($this) eq $type;
}

########################################################################
sub is_array {
########################################################################
  my ($this) = @_;

  return _is_obj( $this, 'ARRAY' );
}

########################################################################
sub is_scalar {
########################################################################
  my ($this) = @_;

  return !ref $this;
}

########################################################################
sub is_hash {
########################################################################
  my ($this) = @_;

  return _is_obj( $this, 'HASH' );
}

########################################################################
sub get_exe_file_list {
########################################################################
  my ($file) = @_;

  my $lines;
  if ($file) {
    ($lines) = process_file(
      $file,
      chomp            => $TRUE,
      skip_blank_lines => $TRUE,
      process          => sub {
        my $f = pop @_;
        $f =~ s/^.*\/(.*)$/bin\/$1/xsm;
        return $f;
      }
    );
  }

  return $lines ? @{$lines} : ();
}

########################################################################
sub get_perl_version {
########################################################################
  my ($requires) = @_;

  my $version;

  if ( !-e $requires ) {
    return;
  }

  process_file(
    $requires,
    chomp   => $TRUE,
    process => sub {
      my $module = pop @_;

      if ( $module !~ /^perl\s+/xsm ) {
        return ();
      }

      ( undef, $version ) = split /\s+/xsm, $module;
      return;
    }
  );

  return $version;
}

########################################################################
sub get_provides {
########################################################################
  my ( $file, %options ) = @_;

  my %provides;

  if ($file) {
    my ($lines) = process_file(
      $file,
      chomp            => $TRUE,
      skip_blank_lines => $TRUE,
      prefix           => 'lib',
      process          => sub {
        my $module = pop @_;
        my $args   = pop @_;

        if ( !$module ) {
          return ();
        }

        my $include_path = $args->{'prefix'};

        if ( $options{'work-dir'} ) {
          $include_path = $options{'work-dir'} . $SLASH . $include_path;
        }

        my $module_version = get_module_version( $module, $include_path );

        my ( $provided_module, $version )
          = @{$module_version}{qw( module version)};

        croak "$module not found in $include_path"
          if !defined $version;

        $provides{$provided_module} = {
          file    => $args->{prefix} . $SLASH . $module_version->{'file'},
          version => $version,
        };

        return $provided_module;
      }
    );
  }
  return %provides;
}

########################################################################
sub get_resources {
########################################################################
  goto &get_json_file;
}

########################################################################
sub get_json_file {
########################################################################
  my ($file) = @_;

  my ($json) = process_file(
    $file,
    chomp       => 1,
    merge_lines => 1
  );

  return decode_json($json);
}

########################################################################
sub write_resources {
########################################################################
  my ( $resources, %args ) = @_;

  my $resources_file;

  if ($resources) {
    $resources_file = 'resources';

    open my $fh, '>', $resources_file
      or croak "could not open resources for writing\n";

    print {$fh} encode_json($resources);

    close $fh
      or croak "could not close file $resources_file\n";
  }

  return %args;
}

########################################################################
sub write_provides {
########################################################################
  my ( $provides, %args ) = @_;

  my $provides_file;

  if ($provides) {
    $provides_file = 'provides';

    open my $fh, '>', $provides_file
      or croak "could not open 'provides' for writing\n";

    foreach my $file ( @{$provides} ) {
      print {$fh} "$file\n";
    }

    close $fh
      or croak "could not close 'provides'\n";

    $args{P} = $provides_file;
  }

  return %args;
}

########################################################################
sub write_makefile {
########################################################################
  my (%options) = @_;

  my $core            = $options{'core-modules'};
  my $module_abstract = $options{'abstract'};
  my $author          = $options{'author'};

  my $module = 'lib/' . make_path_from_module( $options{'module'} );

  local $Data::Dumper::Terse    = $TRUE;
  local $Data::Dumper::Sortkeys = $TRUE;
  local $Data::Dumper::Indent   = 2;
  local $Data::Dumper::Pad      = $SPACE x $INDENT;

  # dependencies
  foreach my $d (qw(requires test-requires build-requires )) {
    $options{$d} = $options{$d} || $d;
  }

  my $pre_req = Dumper get_requires( $options{'requires'}, $core,
    $options{'min-perl-version'} );
  $pre_req = trim($pre_req);
  $pre_req =~ s/\@(\d+)/== $1/xsmg;

  my $test_req = {};

  if ( $options{'test-requires'} && -s $options{'test-requires'} ) {
    $test_req = Dumper get_requires( $options{'test-requires'},
      $core, $options{'min-perl-version'} );
  }
  else {
    $test_req = '{}';
  }

  $test_req = trim($test_req);
  $test_req =~ s/\@(\d+)/== $1/xsmg;

  my $build_req = {};

  if ( $options{'build-requires'} && -s $options{'build-requires'} ) {
    $build_req = Dumper get_requires( $options{'build-requires'},
      $TRUE, $options{'min-perl-version'} );
    $build_req = trim($build_req);
  }

  foreach my $m (qw( ExtUtils::MakeMaker File::ShareDir::Install)) {
    $build_req->{$m} = $build_req->{$m} || $FALSE;
  }

  $build_req = Dumper $build_req;

  my @exe_file_list;

  if ( $options{'exe-files'} && -s $options{'exe-files'} ) {
    @exe_file_list = get_exe_file_list( $options{'exe-files'} );
  }

  my $exe_files = Dumper \@exe_file_list;

  my %provides;

  if ( -e 'provides' ) {
    %provides = get_provides( 'provides', %options );
  }

  my $resources;

  if ( -e 'resources' ) {
    $resources = get_resources('resources');
  }

  my $META_MERGE = 'META_MERGE ' . $FAT_ARROW;

  {
    local $Data::Dumper::Pair = $FAT_ARROW;

    $META_MERGE .= Dumper(
      { 'meta-spec' => { version => 2 },
        'provides'  => \%provides,
        $resources ? ( 'resources' => $resources ) : ()
      }
    );
  }

  my $timestamp        = scalar localtime;
  my $min_perl_version = $options{'min-perl-version'};

  my $min_perl_version_str
    = defined $min_perl_version
    ? qq{MIN_PERL_VERSION => '$min_perl_version',}
    : $EMPTY;

  my $MAKEFILE = <<"END_OF_TEXT";
# autogenerated by $PROGRAM_NAME on $timestamp

use strict;
use warnings;

use ExtUtils::MakeMaker;
use File::ShareDir::Install;

if ( -d 'share' ) {
  install_share 'share';
}

WriteMakefile(
  NAME           => '$options{module}',
  $min_perl_version_str
  AUTHOR         => '$author',
  VERSION_FROM   => '$module',
  ABSTRACT       => '$module_abstract',
  LICENSE        => 'perl',
  PL_FILES       => {},
  EXE_FILES      => $exe_files,
  PREREQ_PM      => $pre_req,
  BUILD_REQUIRES => {
    'ExtUtils::MakeMaker'     => '6.64',
    'File::ShareDir::Install' => $NO_VERSION,
    },
  CONFIGURE_REQUIRES => {
    'ExtUtils::MakeMaker'     => '6.64',
    'File::ShareDir::Install' => $NO_VERSION,
    },
  TEST_REQUIRES => $test_req,
  $META_MERGE
);

package MY;

use File::ShareDir::Install qw( postamble );

END_OF_TEXT

  print $MAKEFILE;

  DEBUG sub { return $MAKEFILE };

  return $SUCCESS;
}

########################################################################
sub write_file_list {
########################################################################
  my (%args) = @_;

  my ( $file_list, $destdir, $project_root )
    = @args{qw(file_list destination project_root)};

  my @expanded_list;

  foreach my $f ( @{$file_list} ) {
    my $fqp = sprintf '%s/%s', $project_root, $f;

    DEBUG Dumper( [ 'write_file_list:', $fqp ] );

    if ( -d $fqp ) {
      find(
        sub {
          if ( !/^[.]+$/xsm ) {
            push @expanded_list, "$File::Find::name $destdir/$_";
          }
        },
        $fqp
      );
      # remove project root since bash script will add it
      for (@expanded_list) {
        s/^$project_root//xsm;
      }
    }
    else {
      # the intent is to cp files to root of distribution (not
      # to install the files during package installation...the
      # exception being if items are installed into share
      # directory
      die
        "ERROR: missing file in list ($fqp) - check your `extra-files` section\n"
        if !-e $fqp;

      my ( $name, $path, $ext ) = fileparse( $fqp, qr/[.][^.]+/xsm );
      push @expanded_list, sprintf '%s %s%s%s', $f, $destdir, $name, $ext;
    }
  }

  return @expanded_list;
}

# file name or hash
# extra-files:
#   share:
#     - README.md
#     - ChangeLog
#   examples: src/examples
# extra-files:
#   - file

########################################################################
sub write_extra_files {
########################################################################
  my (%params) = @_;

  DEBUG('writing extra-files');

  my ( $extra_files, $extra, $project_root )
    = @params{qw(extra_files extra project_root)};

  my %args = %{ $params{args} };

  $extra_files //= [];

  if ( !is_array($extra_files) ) {
    croak "extra-files must be an array!\n"
      . Dumper( [ $extra_files, \%params ] );
  }

  my $extra_files_path = $extra || 'extra-files';

  my @file_list;

  foreach my $e ( @{$extra_files} ) {
    DEBUG Dumper( [$e] );

    if ( !ref $e ) { # simple file
      push @file_list, $e;
    }
    elsif ( is_hash($e) ) { # possibly dest => dir, or dest => list
      foreach my $destdir ( keys %{$e} ) {
        my ($destdir) = keys %{$e};

        my $file_list = $e->{$destdir};

        if ( is_scalar($file_list) ) {
          $file_list = [$file_list];
        }
        elsif ( !is_array($file_list) ) {
          croak 'extra-files args can only be scalar or list';
        }

        push @file_list,
          write_file_list(
          file_list    => $file_list,
          destination  => $destdir,
          project_root => $project_root,
          );
      }
    }
  }

  if (@file_list) {
    open my $fh, '>', $extra_files_path
      or croak "could not append to $extra_files_path\n";

    foreach my $f (@file_list) {
      print {$fh} "$f\n";
    }

    close $fh
      or croak "could not close $extra_files_path\n";
  }

  $args{f} = $extra_files_path;

  return %args;
}

########################################################################
sub parse_path {
########################################################################
  my ( $path, %args ) = @_;

  if ($path) {
    if ( $path->{'recurse'}
      && $path->{'recurse'} =~ /(yes|no)/ixsm ) {
      $args{R} = $path->{'recurse'};
    }
    elsif ( $path->{'recurse'} ) {
      croak "use only yes or no for 'recurse' option\n";
    }

    # -l
    if ( $path->{'pm_module'} ) {
      $args{l} = $path->{'pm_module'};
    }

    # -e
    if ( $path->{'exe_files'} ) {
      $args{e} = $path->{'exe_files'};
    }

    # -S
    if ( $path->{'scripts'} ) {
      $args{S} = $path->{'scripts'};
    }

    # -t
    if ( $path->{'tests'} ) {
      $args{t} = $path->{'tests'};
    }
  }

  return %args;
}

########################################################################
sub parse_dependencies {
########################################################################
  my ( $dependencies, %args ) = @_;

  if ($dependencies) {
    croak 'malformed buildspec.yml file - dependencies section with no keys?'
      if !keys %{$dependencies};

    $dependencies->{core_modules} //= 'no';

    if ( $dependencies->{path} ) { # deprecatd
      $args{D} = $dependencies->{path};
      warn "path is deprecated: use requires\n";
    }

    if ( $dependencies->{requires} ) {
      $args{D} = $dependencies->{requires};
    }

    if ( $dependencies->{test_requires} ) {
      $args{T} = $dependencies->{test_requires};
    }

    if ( $dependencies->{build_requires} ) {
      $args{B} = $dependencies->{build_requires};
    }

    if ( $dependencies->{core_modules} eq 'yes' ) {
      $args{c} = $EMPTY;
    }

    if ( my $resolver = $dependencies->{resolver} ) {
      if ( $resolver eq 'scandeps' ) {
        $args{s} = $EMPTY;
      }
      else {
        $args{r} = $dependencies->{resolver};
      }
    }

    if ( $args{D} && $args{r} ) {
      croak "use either path or resolver for dependencies, but not both\n";
    }
  }

  return %args;
}

########################################################################
sub parse_include_version {
########################################################################
  my ( $version, %args ) = @_;

  if ( defined $version ) {
    if ( $version =~ /(no|0|off)/xsm ) {
      $args{A} = $EMPTY;
    }
  }
  return %args;
}

########################################################################
sub parse_project {
########################################################################
  my ( $project, %args ) = @_;

  if ($project) {
    if ( $project->{author} ) {
      $args{a} = $project->{author}->{name};

      if ( $project->{author}->{mailto} ) {
        $args{a} .= ' <' . $project->{author}->{mailto} . '>';
      }

      $args{a} = sprintf q{'%s'}, $args{a};
    }

    # -d
    if ( $project->{description} ) {
      $args{d} = sprintf q{'%s'}, $project->{description};
    }

    # -g
    if ( $project->{git} ) {
      $args{g} = $project->{git};
    }
  }

  return %args;
}

########################################################################
sub parse_pm_module {
########################################################################
  my ( $pm_module, %args ) = @_;

  if ($pm_module) {
    $args{m} = $pm_module;
  }
  return %args;
}

########################################################################
sub get_buildspec {
########################################################################
  my ($file) = @_;

  croak 'file not found or empty ' . $file . $NL
    if !-s $file;

  my $buildspec = eval { YAML::Tiny->read($file)->[0]; };

  croak 'could not read ' . $file . $NL . $EVAL_ERROR . $NL
    if $EVAL_ERROR || !$buildspec;

  return $buildspec;
}

########################################################################
sub parse_buildspec {
########################################################################
  my (%options) = @_;

  my $file = $options{buildspec};

  my $buildspec = get_buildspec($file);

  croak 'bad build file'
    if !validate_object( $buildspec, $options{'yaml-spec'} );

  my %args;

  if ( $buildspec->{'min-perl-version'} ) {
    $args{M} = $buildspec->{'min-perl-version'};
  }

  if ( $options{'project-root'} ) {
    $args{H} = $options{'project-root'};
  }

  %args = parse_project( $buildspec->{project}, %args );

  %args = parse_pm_module( $buildspec->{pm_module}, %args );

  %args = parse_include_version( $buildspec->{'include-version'}, %args );

  %args = parse_dependencies( $buildspec->{dependencies}, %args );

  %args = parse_path( $buildspec->{path}, %args );

  %args = write_extra_files(
    extra_files  => $buildspec->{'extra-files'},
    extra        => $buildspec->{extra},
    args         => \%args,
    project_root => $options{'project-root'}
  );

  %args = write_provides( $buildspec->{provides}, %args );

  %args = write_resources( $buildspec->{resources}, %args );

  # set boolean args from options

  my @boolean_args
    = qw( verbose v cleanup !x scandeps s require-versions !A );

  foreach my $pair ( pairs @boolean_args ) {
    my ( $key, $value ) = @{$pair};

    if ( $value =~ /^\!(.*)$/xsm ) {
      if ( $options{$1} ) {
        delete $options{$1};
      }
    }
    elsif ( $options{$key} ) {
      $args{$value} = $EMPTY;
    }
  }

  # set value args from buildspec
  foreach my $pair ( pairs qw( destdir o extra f ) ) {
    my ( $key, $value ) = @{$pair};

    if ( $buildspec->{$key} ) {
      $args{$value} = $buildspec->{$key};
    }
  }

  foreach my $k ( keys %args ) {
    $args{ $DASH . $k } = $args{$k};
    delete $args{$k};
  }

  return %args;
}

########################################################################
sub get_requires {
########################################################################
  my ( $requires, $core_modules, $min_perl_version ) = @_;

  my %modules;

  process_file(
    $requires,
    chomp            => $TRUE,
    skip_blank_lines => $TRUE,
    filter           => sub {
      my ( $fh, $all_lines, $args, $line ) = @_;
      $line = filter( $fh, $all_lines, $args, $line );

      return ()    if $line && $line =~ /^perl\s+/xsm;
      return $line if !defined $line;
      return $line if $core_modules;
      return $line if $line =~ /^[+]/xsm;

      return is_core( $line, $min_perl_version )
        ? undef
        : $line;
    },
    process => sub {
      my $line = pop @_;

      $line =~ s/^[+]([^+]*)$/$1/xsm;

      my ( $module, $version ) = split /\s/xsm, $line;
      $version = $version || '0';

      $modules{$module} = $version;

      return $line;
    }
  );

  return \%modules;
}

########################################################################
sub get_modules {
########################################################################
  my ($module_list) = @_;

  my ($modules) = process_file( $module_list, chomp => $TRUE );

  return $modules;
}

########################################################################
sub get_yaml_specfile {
########################################################################
  my ($options) = @_;

  my ($lines) = process_file(
    *DATA,
    chomp     => $TRUE,
    next_line => sub {
      my ( $fh, $all_lines, $args ) = @_;

      my $line = <$fh>;

      return if $line =~ /^\=pod/xsm; # signal end of file

      return $line;
    }
  );

  return Load join "\n", @{$lines};
}

########################################################################
sub validate_object {
########################################################################
  my ( $obj, $spec, $err ) = @_;

  $err = $err // 0;

  if ( reftype($obj) eq 'HASH' ) {
    foreach my $k ( keys %{$obj} ) {
      if ( !exists $spec->{$k} ) {
        carp "ERROR: not a valid key ($k)\n" . Dumper [ $k, $spec ];
        $err++;
      }

      if ( ref $spec->{$k} ) {
        if ( !ref $obj->{$k}
          || reftype( $obj->{$k} ) ne reftype( $spec->{$k} ) ) {
          warn "ERROR: wrong type for ($k) - $k must be "
            . reftype( $spec->{$k} ) . "\n";
          $err++;
        }
        else {
          validate_object( $obj->{$k}, $spec->{$k}, $err );
        }
      }
    }
  }
  else { # just validate arrays are arrays for now, deep dive TBD
    $err = reftype($spec) eq 'ARRAY';
  }

  return $err ? $FALSE : $TRUE;
}

# +-------------------------+
# | MAIN SCRIPT STARTS HERE |
# +-------------------------+

sub main {
  my %options = ( cleanup => 1, pager => 1 );

  GetOptions(
    \%options,              'abstract|A=s',
    'author|a=s',           'buildspec|b=s',
    'build-requires|B=s',   'cleanup!',
    'core-modules!',        'dryrun',
    'debug|D',              'exe-files=s',
    'help|h',               'log-level|l=s',
    'module|m=s',           'require-versions|R!',
    'min-perl-version|M=s', 'requires|r=s',
    'pager|P!',             'project-root|p=s',
    'scandeps|s',           'test-requires=s',
    'verbose|V',            'version|v',
    'work-dir|w=s',
  );

  if ( $options{help} ) {
    help( \%options );

    exit;
  }

  $options{'project-root'} //= getcwd;

  $options{'yaml-spec'} = get_yaml_specfile;

  if ( $options{'min-perl-version'} ) {
    $options{'min-perl-version'}
      = version->parse( $options{'min-perl-version'} )->stringify;
  }
  else {
    if ( $options{requires} ) {
      $options{'min-perl-version'} = get_perl_version( $options{requires} );
    }

    # $options{'min-perl-version'} //= $DEFAULT_PERL_VERSION;
  }

  if ( !exists $options{'require-versions'} ) {
    $options{'require-versions'} = $TRUE;
  }

  if ( $options{version} ) {
    print $PROGRAM_NAME . ' v' . $VERSION . $NL;

    exit $SH_SUCCESS;
  }

  my $log_level = $options{'log-level'};

  if ($log_level) {
    if ( $log_level =~ /\A[1-5]\z$/xsm ) {
      $log_level = ( $ERROR, $WARN, $INFO, $DEBUG, $TRACE )[ $log_level - 1 ];
    }
    else {
      $log_level = {
        ERROR => $ERROR,
        WARN  => $WARN,
        INFO  => $INFO,
        DEBUG => $DEBUG,
        TRACE => $TRACE,
      }->{ uc $options{'log-level'} };
    }

  }
  elsif ( $options{debug} ) {
    $log_level = $DEBUG;
  }

  if ( !$log_level ) {
    $log_level = $ERROR;
  }

  Log::Log4perl->easy_init($log_level);

  if ( $options{buildspec} ) {
    my %args = parse_buildspec(%options);

    if ($log_level) {
      $args{'-L'} = {
        $ERROR => 1,
        $WARN  => 2,
        $INFO  => 3,
        $DEBUG => 4,
        $TRACE => 5,
      }->{$log_level};
    }

    if ( !$options{dryrun} ) {
      exec '@bindir@/make-cpan-dist ' . join $SPACE, %args; ## no critic (ValuesAndExpressions::RequireInterpolationOfMetachars)
    }
    else {
      print '@bindir@/make-cpan-dist ' . ( join $SPACE, %args ) . $NL; ## no critic (ValuesAndExpressions::RequireInterpolationOfMetachars)
    }
  }
  else {
    croak 'no module specified'
      if !$options{module};

    croak 'no dependencies'
      if !$options{requires};

    $options{author} = $options{author} // 'Anonyous <anonymous@example.com>';
    $options{abstract} = $options{abstract} // 'my awesome Perl module!';

    if ( !write_makefile(%options) ) {
      help();
      exit $SH_FAILURE;
    }
  }

  exit $SH_SUCCESS;
}

1;

__DATA__
---
version: "@PACKAGE_VERSION@"
min-perl-version: "type:string"
project:
  git: "type:string"
  description: "type:string"
  author:
    name: "type:string"
    mailto: "type:string"
pm_module:
include-version: "type:boolean"
dependencies:
  resolver: "type:string"
  path: "type:string"
  requires: "type:string"
  test_requires: "type:string"
  build_requires: "type:string"
  core_modules: "type::boolean"
path:
  recurse: "type:boolean"
  pm_module: "type:string"
  tests: "type:string"
  exe_files: "type:string"
  scripts: "type:string"
destdir: "type:string"
extra: "type:string"
extra-files:
provides: "type:string"
resources:
  homepage: "type:string"
  bugtracker:
    web: "type:string"
    mailto: "type:string"
  repository:
    url: "type:string"
    web: "type:string"
    type: "type:string"

=pod

=head1 NAME

make-cpan-spec.pl

=head1 SYNOPSIS

 make-cpan-dist.pl options

 make-cpan-dist.pl \
   --module Some::Module \
   --author 'Rob Lauer <rlauer6@comcast.net>' \
   --abstract 'my wonderful module'

=head1 DESCRIPTION

Utility that is part of a toolchain to create a simple CPAN
distribution. This utility is typically called from the C<bash> helper
script of the same name or can be passed a build specfication file in
YAML format. You may I<not> find this easier to use than crafting your
own CPAN distributions with the various tools available for
such.

The helper C<bash> script (F<make-cpan-dist>) does most of the heavy lifting,
doing things for you like attempting to resolve dependencies and
packaging the distribution as a tarball.

In general you should consider using the C<bash> script or create a
F<buildspec.yml> file and invoke this utility with the C<-b> option
described below rather than calling this script directly.

Try C<make-cpan-dist.pl -h> to get some helpful instructions.

=head2 Using the Bash Script

Assuming you have a module named C<Foo::Bar> in a directory named
F<lib> and some tests in a directory named F<t>, you might try:

 make-cpan-dist -l lib -t t -m Foo::Bar \
  -a 'Rob Lauer <rlauer6@comcast.net>' -d 'the Foo::Bar module!'

=head2 Using F<buildspec.yml>

 make-cpan-dist.pl -b buildspec.yml

Calling this utility directly with the C<-b> option will invoke the
C<bash> script and is probably the preferred way of using this
toolchain. The format of the YAML build file is described below.

If you insist on using this utility directly anyway, read on.

=head1 OPTIONS

=over 5

=item -A, --abstract

Short description of the module purpose.

=item -a, --author

When supplying the author on the command line, include the email
address in angle brackets as shown in the example.

Example: -a 'Rob Lauer <rlauer6@comcast.net>'

=item -B, --build-requires

Name of the dependeny file list for building the distribution.

=item -b, --buildspec

Name of a build specification file.  The build specification file is
used B<instead> of options. See L</Build Specificaton Format>.

=item -c, --cleanup

Cleanup temp directories and files.  Default is to cleanup, use
--no-cleanup if you want to examine some of the temporary files.

=item -l, --log-level

Log level.

Valid values: error|warn|info|debug

=item -m, --module

Name of the module

=item -M, --module-list

List of modules to include in distribution

=item -R, --require-versions, --no-require-versions

Whether to add version numbers to dependencies.

default: --require-versions

=item -r, --requires

Name of the dependency file list if other than F<requires>.

=item -s, --scandeps

Use scandeps for dependency checking

=item -t, --test-requires

Name of the test dependencies file if other than F<test-requires>.

=item -v, --version

Returns the version of this script

=back

Note: Set the environment variable C<PRESERVE_MAKEFILE> if you want
the script to preserve the F<Makefile.PL>.  It will be copied to your
current working directory.

=head1 Build File Format

  version: @PACKAGE_VERSION@
  project:
    git: https://github.com/rlauer6/perl-Amazon-Credentials
    description: "AWS credentials discoverer"
    author:
      name: Rob Lauer
      mailto: rlauer6@comcast.net
  pm_module: Amazon::Credentials
  include-version: no
  dependencies:
    resolver: scandeps
    path: requires
    requires: requires
    test_requires: test-requires
  path:
    recurse: yes
    pm_module: src/main/perl/lib
    tests: src/main/perl/t
    exe_files: src/main/perl/bin
  destdir: 
  extra: extra-files
  extra-files:
    -file
    /usr/local/share/my-project: file
    /usr/local/share/my-project:
      -file
  provides: provides
  resources:
    homepage: 'http://github.com/rlauer6/perl-Amazon-API'
    bugtracker:
      web: 'http://github.com/rlauer6/perl-Amazon-API/issues'
      mailto: rlauer6@comcast.net
    repository:
      url: 'git://github.com/rlauer6/perl-Amazon-API.git'
      web: 'http://github.com/rlauer6/perl-Amazon-API'
      type: 'git'

The sections are described below:

=over 10

=item project

=over 15

=item git

The path to the C<git> project. When the project is cloned, the
utility will look for F<configure.ac> and if found will attempt to
build the project in usual I<autoconfiscated> manner:

 git clone {project}
 autoconf -i --force
 ./configure
 make

...otherwise the project will simply be cloned and it will be assumed
the Perl module is somewhere to be found in the project tree.  You
should make sure that you set the C<path> section accordingly so that
the utility now knows were to find your Perl module though.

=item description

The description of the module as it will be appear in the CPAN
repository.

=item author

The I<author> section should contain a name and email address.

=over 20

=item name

The author's name.

=item mailto

The author's email address.

=back

=back

=item pm_module

The name of the Perl module.

=item include-version

If dependencies are resolved automatically include the version
number. To disable set to no.

default: yes

=item dependencies (optional)

The I<dependencies> section, if present may contain the fully
qualified path to a file that contains a list of dependencies. If
the name of the file is F<cpanfile>, then the file is assumed to be in
I<cpanfile> format, otherwise the file should be a simple list of Perl
module names optionally followed by a version number.

 Amazon::Credentials 1.15

By default, the scripts will look for F</usr/lib/rpm/perl.req> as the
dependency resolver, however you can override this by specifying the
name of program that will produce a list of modules.  If you specify
the special name I<scandeps>, the scripts will use F<scandeps.pl>.

=over 15

=item path (deprecated)

=item requires

Fully qualified path to a dependency list for module.

=item test_requires

Fully qualified path to a dependency list for tests.

=item build_requires

Fully qualified path to a dependency list for build.

=item resolver (optional)

Name of a program that will provide a list of depenencies when passed
a module name. Use the special name C<scandeps> to use Perl's
C<scandeps.pl>.  When using C<scandeps.pl>, the C<-R> option will be
used to prevent C<scandeps.pl> from recursing. Neither
C</usr/lib/rpm/perl.req> or C<scandeps.pl> are completely
reliable. Your methodology might be to use these to get a good start
on a file containing dependencies and then add/subtract as required
for your use case.

When preparing the list of files to list as requirements in the
C<PREREQ_PM> section of the C<Makefile.PL>, the script will
automatically remove any modules that are already included with Perl.

=back

=item path (optional)

=over 15

=item pm_module

The path where the Perl module to be packaged can be found.  By
default, the current working directory will be searched.

=item recurse (optional)

Specifies whether to or not to looks in subdirectories of path
specified by C<pm_module> for additional modules to package.  The
default is 'yes'.  Valid values are "yes" or "no".

=item tests (optional)

The path where tests to be specified in the F<Makefile.PL> will be
found.

=back

=item provides (optional)

By default the package will provide the module to be packaged and any
files included that were found if the C<recurse> option was specified.

=item resources (optional)

Keys to add to the resources section.  This section is merged into the
META_MERGE key.  See L<https://metacpan.org/pod/CPAN::Meta::Spec> for
more details.

=item extra (optional)

Name of a file that contains a list of files to be included in the
package. These files are included in the package but not installed.

=item extra-files (optional)

List of files to be included in package.

Example:

 extra-files:
   - ChangeLog
   - README
   -
     examples: src/examples

 extra-files:
   - ChangeLog
   - README
   - examples:
      - src/examples/foo.pl
      - src/examples/boo.pl

If you include in your C<extra-files> specification, a 'share' directory,
then that directory will be installed as part of the distribution.

I<This feature is only available when the directory is specified in the
C<extra-files> key of the F<buildspec.yml> file. Files specified in
the C<extra-files> section will be copied to the root of the distribution
tarball and will not be automatically installed.>

=item exe_files (optional)

Path to look for C<.pl> and C<.sh> files to package.

=back


=head1 DEPENDENCIES

By default the script will look for dependencies in files named
F<requires> and F<test-requires>.  These are typically created by the
C<bash> script and the files are transparent to you during the
creation of the distribution.

You can specify a different name for the files with the C<-r> and
C<-t> options.

 B<You must however have a file that contains the dependency list.>

Again, if you use the C<bash> script that invokes this utility or are
calling this utility with a F<buildspec.yml> file, these files will be
I<automatically> created for you based on your options.  If you provide
your own F<requires> or F<test-requires> file, modules should be
listed as:

  module-name version

Example:

 AWS::Signature4 1.02
 ...

=head1 AUTHOR

Rob Lauer - <rlauer6@comcast.net>

=cut
